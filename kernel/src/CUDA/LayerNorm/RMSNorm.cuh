#pragma once
#include <cassert>
#include "../reduce/Reduce.cuh"
#include <cuda_bf16.h>
#include <cuda_fp16.h>

template <typename T>
__global__ void rmsnorm(T *__restrict__ out,         // [B, T, C]
                        const T *__restrict__ x,     // [B, T, C]
                        const T *__restrict__ gamma, // [C]
                        int C, float eps) {
	int bt = blockIdx.x;
	float local_sum = 0.0f;

	constexpr int VEC = sizeof(float4) / sizeof(T);
	for (int i = threadIdx.x * VEC; i + VEC <= C; i += blockDim.x * VEC) {
		float4 xc_v = *reinterpret_cast<const float4 *>(&x[bt * C + i]);
		T *elem = reinterpret_cast<T *>(&xc_v);
		for (int j = 0; j < VEC; ++j) {
			local_sum += static_cast<float>(elem[j]) *
			             static_cast<float>(elem[j]);
		}
	}
	for (int c = C / VEC * VEC + threadIdx.x; c < C; c += blockDim.x) {
		float xc = static_cast<float>(x[bt * C + c]);
		local_sum += xc * xc;
	}

	float sum_x2 = block_sum(local_sum);
	float rstd = rsqrtf(sum_x2 / C + eps);

	for (int c = threadIdx.x * VEC; c + VEC <= C; c += blockDim.x * VEC) {
		float4 xc_v = *reinterpret_cast<const float4 *>(&x[bt * C + c]);
		float4 gamma_v = *reinterpret_cast<const float4 *>(&gamma[c]);
		T *x_elems = reinterpret_cast<T *>(&xc_v);
		T *g_elems = reinterpret_cast<T *>(&gamma_v);
		float4 out_v;
		T *o_elems = reinterpret_cast<T *>(&out_v);
		for (int j = 0; j < VEC; ++j) {
			float norm = (static_cast<float>(x_elems[j])) * rstd;
			float g = static_cast<float>(g_elems[j]);
			o_elems[j] = static_cast<T>(norm * g);
		}
		*reinterpret_cast<float4 *>(&out[bt * C + c]) = out_v;
	}

	for (int c = C / VEC * VEC + threadIdx.x; c < C; c += blockDim.x) {
		float xc = static_cast<float>(x[bt * C + c]);
		float norm = xc * rstd;
		float g = static_cast<float>(gamma[c]);
		out[bt * C + c] = static_cast<T>(norm * g);
	}
}

template <typename T>
void launch_RMSNorm_forward(T *__restrict__ out,         // [B, T, C]
                            const T *__restrict__ x,     // [B, T, C]
                            const T *__restrict__ gamma, // [C]
                            int B, int seq_len, int C, float eps) {
	int block = 256;
	int grid = B * seq_len;
	rmsnorm<<<grid, block>>>(out, x, gamma, C, eps);
}

// dtype 契约: 0=f32 1=bf16 2=f16,与 backend Dtype::TAG 一致
extern "C" void rmsnorm_forward(
		int dtype, void *out, const void *x, const void *gamma,
		int B, int seq_len, int C, float eps
) {
	switch (dtype) {
		case 0: launch_RMSNorm_forward((float *)out, (const float *)x, (const float *)gamma, B, seq_len, C, eps); break;
		case 1: launch_RMSNorm_forward((__nv_bfloat16 *)out, (const __nv_bfloat16 *)x, (const __nv_bfloat16 *)gamma, B, seq_len, C, eps); break;
		case 2: launch_RMSNorm_forward((half *)out, (const half *)x, (const half *)gamma, B, seq_len, C, eps); break;
		default: assert(false && "unknown dtype");
	}
}
